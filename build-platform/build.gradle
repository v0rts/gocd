plugins {
  id 'java-platform'
  id 'maven-publish'
}

description = 'Internal platform constraining dependency resolution for GoCD'

javaPlatform {
  allowDependencies()
}

// Control versions of transitive dependencies that can have conflicts across various projects
dependencies {
  // Use BOMs to control versions of dependencies for other projects where possible.
  // This is following https://docs.gradle.org/current/userguide/platforms.html#sub:bom_import
  api enforcedPlatform(project.deps.jacksonBom)
  api platform(project.deps.jaxbBom)

  constraints {
    api project.deps.angusActivation
    api project.deps.commonsCodec
    api('commons-io:commons-io') {
      version {
        // Prevent early upgrade from transitive dependencies (e.g. commons-fileupload)
        strictly(project.versions.commonsIO as String)
      }
    }
    api project.deps.commonsPool
    api project.deps.commonsCollections
    api project.deps.guava
    api project.deps.jetBrainsAnnotations
    api project.deps.objenesis
    api('org.slf4j:slf4j-api') {
      version {
        // Prevent early upgrade from transitive dependencies due to lack of slf4J BOM to declare as an enforcedPlatform
        strictly(project.versions.slf4j as String)
      }
    }
  }

  api enforcedPlatform(project.deps.junit5Bom)
  api enforcedPlatform(project.deps.mockitoBom)
  api enforcedPlatform(project.deps.testcontainersBom)
}

publishing {
  publications {
    maven(MavenPublication)
  }
}
